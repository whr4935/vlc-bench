apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    signingConfigs {
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.videolan.vlcbenchmark"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 4
        versionName "0.1.4"
        buildConfigField("String", "VLC_VERSION", "\"3.1.0\"")
        vectorDrawables.useSupportLibrary = true
        resValue "string", "build_time", buildTime()
        resValue "string", "build_host", hostName()
        resValue "string", "build_revision", revision()
        signingConfig signingConfigs.debug
    }
    // right now debug_prod & release mode use the same URL ressources
    // but in the future there will be a debug infra
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "build_api_address", "https://bench.videolabs.io/api/benchmarks"
            resValue "string", "file_location_url", "https://streams.videolan.org/benchmark/"
            resValue "string", "config_file_location_url", "https://streams.videolan.org/benchmark/config.json"
            resValue "string", "vlc_package_name", "org.videolan.vlc"
        }
        release_alpha.initWith(buildTypes.release)
        release_alpha {
            resValue "string", "vlc_package_name", "org.videolan.vlc.bench_alpha"
        }
        // debug is to use with local resources
        debug {
            resValue "string", "build_api_address", "http://" + apiAddress() + ":8080/benchmarks"
            resValue "string", "file_location_url", "ftp://" + apiAddress() + "/"
            resValue "string", "config_file_location_url", "ftp://" + apiAddress() + "/config.json"
            resValue "string", "vlc_package_name", "org.videolan.vlc.debug"
        }
        // debug_prod is debug mode with the urls resources of release
        debug_prod.initWith(buildTypes.debug)
        debug_prod {
            resValue "string", "build_api_address", "https://bench.videolabs.io/api/benchmarks"
            resValue "string", "file_location_url", "https://streams.videolan.org/benchmark/"
            resValue "string", "config_file_location_url", "https://streams.videolan.org/benchmark/config.json"
            debuggable true
        }
    }
    applicationVariants.all { variant ->
        //Custom APK name
        variant.outputs.all { output ->
            outputFileName = "VLCBenchmark-" + variant.versionName + "_" + variant.baseName + ".apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def lifecycle_version = "2.0.0-rc01"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'com.google.android.gms:play-services-auth:15.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    testImplementation 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}

// Tmp solution to find the ip address of the computer building VLCBenchmark
// ideally should use shell script + environment variables
static def apiAddress() {
    InetAddress result = null
    Enumeration<NetworkInterface> interfaces = NetworkInterface.getNetworkInterfaces()
    while (interfaces.hasMoreElements()) {
        NetworkInterface anInterface = interfaces.nextElement()
        if (anInterface.displayName.contains("docker") ||
                anInterface.displayName.contains("virbr") ||
                anInterface.displayName.contains("br-")) {
            continue
        }
        Enumeration<InetAddress> addresses = anInterface.getInetAddresses()
        while (addresses.hasMoreElements()) {
            InetAddress address = addresses.nextElement();
            if (!address.isLoopbackAddress()) {
                if (address.isSiteLocalAddress()) {
                    return address.getHostAddress()
                } else if (result == null) {
                    result = address
                }
            }
        }
    }
    return (result != null ? result : InetAddress.getLocalHost()).getHostAddress()

}

static def hostName() {
    return "${System.getProperty("user.name")}@${InetAddress.localHost.hostName}"
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def revision() {
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = code
    }
    return code.toString()
}
